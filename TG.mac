fini
/clear
/PREP7
/UNITS,SI
SMRT,OFF

thknp=100e-6		!Thickness of melted layer = 100 um
thkgls=2500e-6	 	!Thickness of powder substrate = 2500 um
widthnp=10e-3
divwdth=16



ET,1,SOLID70            ! 3-D THERMAL SOLID ELEMENT
ET,2,SURF152,,,,1,1     ! 3-D THERMAL SURFACE EFFECT ELEMENTS
KEYOPT,2,9,1            ! RADIATION OPTION
R,2,1,0.1712E-8         ! FORM FACTOR = 1, STEFAN-BOLTZMANN CONSTANT
!Material 2 (Radiation heat sink) property
MP,EMIS,2,1             ! BLACK BODY EMISSIVITY
!Material 3 (Sintered powder) property
MP,KXX,3,1300 ! Thermal conductivity
MP,C,3,960 ! Specific heat capacity
MP,DENS,3,2810 ! Density

!Material 1 (Unsintered powder) property
MP,KXX,1,1550
MP,C,1,960          
MP,DENS,1,2810

!TEST: different dens Material 4 (Sintered powder2) property
!MP,KXX,4,1300 ! Thermal conductivity
!MP,C,4,960 ! Specific heat capacity
!MP,DENS,4,2210 ! Density




BLOCK,,widthnp,,thknp,,widthnp		!melted layer
BLOCK,,widthnp,,-thkgls,,widthnp !powder layer
VGLUE,all
/PNUM,VOLU,1
VPLOT

! Mseh melted layer
TYPE,1
REAL,1
MAT,1
LSEL,s,loc,y,0,thknp
LSEL,r,loc,x,0,0
LSEL,r,loc,z,0,0
LESIZE,all,,,2

LSEL,s,loc,x,0,widthnp
LSEL,r,loc,y,0,0
LSEL,r,loc,z,0,0
LESIZE,all,,,divwdth

LSEL,s,loc,z,0,widthnp
LSEL,r,loc,x,0,0
LSEL,r,loc,y,0,0
LESIZE,all,,,divwdth

VSEL,s,loc,y,0,thknp
VMESH,all                 ! MESH WITH A SINGLE SOLID70 ELEMENT


! Mesh powder layer
VSEL,s,loc,y,0,-thkgls
ESIZE,widthnp/divwdth
VMESH,all
allsel


! Set the area of melted part

!Circle part
!Change the coordinate system and position
!clocal,11,cylin,widthnp/2,0,widthnp/2,0,90
!csys,11
!ESEL,s,cent,x,0,widthnp*1/3 ! can be 1/6, 1/5, 1/4, 1/3
!csys,0
!eplot

!Square part
ESEL,s,cent,x,widthnp*1/4,widthnp*3/4
ESEL,r,cent,z,widthnp*1/4,widthnp*3/4
!eplot

!Change the material of selected element
EMODIF,all,MAT,3
ESEL,all

!TEST: different dens
!csys,11
!ESEL,s,cent,x,0,widthnp*1/6 ! can be 1/6, 1/5, 1/4, 1/3
!csys,0
!EMODIF,all,MAT,4
!ESEL,all


!Add EXTRA "SPACE" NODE FOR RADIATION
TYPE,2
REAL,2
MAT,2
N,100000,2.5e-3,-5e-3,2.5e-3             
ESURF,100000               ! GENERATE SURF152 ELEMENTS

/VIEW,1,1,2,3   
/ANG,1 
FINISH


/SOLU
ANTYPE,TRANS            ! TRANSIENT ANALYSIS

! SPECIFY SURROUNDING ABSOLUTE TEMPERATURE
D,100000,TEMP,25

! Initial heated temperature on top surface
NSEL,s,loc,y,0,thknp
nplot
IC,all,temp,100			

! Initial room temperature on bottom
NSEL,inve
nplot
IC,all,temp,25
allsel


! Set symmetry constraints on four sides
ASEL,s,loc,x,0,0
ASEL,a,loc,z,0,0
ASEL,a,loc,x,widthnp,widthnp
ASEL,a,loc,z,widthnp,widthnp
APLOT
DA,all,symm


! Set the time range and time step for transient analysis
time_stp=0.0002  ! 0.00001
time_end=0.01 ! 0.0005
AUTOTS,ON            ! automatic time stepping   
KBC,1                   ! STEP SURROUNDING TEMPERATURE IN FIRST TIME STEP
DELTIM,time_stp,time_stp,time_stp! INITIAL (MINIMUM) INTEGRATION TIME STEP
OUTRES,,ALL
OUTPR,NSOL,LAST
TIME,time_end	                ! TRANSIENT TIME SPAN
TINTP,,,,0.5,,0.2
/OUT,SCRATCH
SOLVE
FINISH

/post26
NUMVAR,divwdth+2

! Open the file
/com
/OUTPUT,'TG_data','csv'
*VWRITE,'TIME','TEMP1','TEMP2','TEMP3','TEMP4','TEMP5','TEMP6','TEMP7','TEMP8','TEMP9','TEMP10','TEMP11','TEMP12','TEMP13','TEMP14','TEMP15','TEMP16','TEMP17'  
%C, %C, %C, %C, %C, %C, %C, %C, %C, %C, %C, %C, %C, %C, %C, %C, %C, %C
/OUTPUT,TERM
	
! Get temp data from node line
*DIM,node_temp,TABLE,time_end/time_stp,divwdth+1
*DO,ii,0,divwdth
	/post26
	NSEL,s,loc,y,thknp
	NSEL,r,loc,z,widthnp*ii/divwdth
	nplot
	
	*VGET,parr_nodes_num,node,,nlist
		
	
	*DO,i,1,divwdth+1
		NSOL,i+1,parr_nodes_num(i),temp,,TMP,
		VGET,node_temp(1,i-1),i
	*ENDDO
	VGET,node_temp(1,divwdth+1),divwdth+2
	
	/com
	/OUTPUT,'TG_data','csv',,APPEND
	*VWRITE,node_temp(1,0),node_temp(1,2),node_temp(1,3),node_temp(1,4),node_temp(1,5),node_temp(1,6),node_temp(1,7),node_temp(1,8),node_temp(1,9),node_temp(1,10),node_temp(1,11),node_temp(1,12),node_temp(1,13),node_temp(1,14),node_temp(1,15),node_temp(1,16),node_temp(1,17),node_temp(1,1)
	%G, %G, %G, %G, %G, %G, %G, %G, %G, %G, %G, %G, %G, %G, %G, %G, %G, %G
	/OUTPUT,TERM
	FINISH
*ENDDO

! Get ANTIME result
!/post1
!PLNS,TEMP,  
!ANTIME,time_end/time_stp,0.5, ,1,2,0,time_end

