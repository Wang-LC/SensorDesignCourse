/clear
/batch,list
/prep7

w=0.3 ! m
h=0.3 ! m

epsr_air=1
epsr_plc=2

ELTR_NUM=4


ET,1,121 ! 8-node 2-D electrostatic element
MP,perx,1,epsr_air ! Electric relative permittivities = epsr_air
MP,perx,2,epsr_plc ! Electric relative permittivities = epsr_plc
EMUNIT,epzro,8.854e-6 ! Set free-space permittivity for Î¼MKSV units

! Build a 2-D square
BLC4,0,0,w,h


! Mesh air region by 3*3
LESIZE,ALL,,,3,,1,,,1,
AMESH,all


! Assign node components to 4 conductors
NSEL,s,loc,y,0 ! _
CM,cond1,node           
NSEL,s,loc,x,0 !|
CM,cond2,node           
NSEL,s,loc,y,h ! -
CM,cond3,node           
NSEL,s,loc,x,w !  |
CM,cond4,node     
ALLSEL,all


! Get DATA_EMPTY
*dim,DATA_EMPTY,array,ELTR_NUM-1,ELTR_NUM
finish
/solu
cmatrix,1,'cond',4,1     ! Compute capacitance matrix coefficients	
finish
! Load CMATRIX results into a customized matrix
*do,ii,1,ELTR_NUM-1 
	*do,jj,ii+1,ELTR_NUM
		/post1
		DATA_EMPTY(ii,jj)=CMATRIX(ii,jj,2)
		finish
	*enddo
*enddo


! Get DATA_FULL
/PREP7
*dim,DATA_FULL,array,ELTR_NUM-1,ELTR_NUM
ESEL,s,,,all
EMODIF,all,MAT,2,
ALLSEL,all
finish
/solu
cmatrix,1,'cond',4,1     ! Compute capacitance matrix coefficients	
finish
! Load CMATRIX results into a customized matrix
*do,ii,1,ELTR_NUM-1 
	*do,jj,ii+1,ELTR_NUM
		/post1
		DATA_FULL(ii,jj)=CMATRIX(ii,jj,2)
		finish
	*enddo
*enddo

! Change them back
/PREP7
ESEL,s,,,all
EMODIF,all,MAT,1,
ALLSEL,all
finish


! Get DATA_RES and DATA_RES_NORMAL by changing Electric relative permittivities one by one
*dim,DATA_RES,array,ELTR_NUM-1,ELTR_NUM
*dim,DATA_RES_NORMAL,array,ELTR_NUM-1,ELTR_NUM

! Open and write the first element into the file
ELEM_DIV=0
/com
/OUTPUT,'ECT_RES','csv'
*VWRITE,ELEM_DIV     !ELEM_DIV is an integer, number of electrodes in the model
%I
! integer
/OUTPUT,TERM ! stop writing

*do,ee,1,9
	
	! Chenge material of element one by one
	/PREP7
	ESEL,s,,,ee
	EMODIF,all,MAT,2,
	ALLSEL,all
	finish
	/solu
	cmatrix,1,'cond',4,1     ! Compute capacitance matrix coefficients	
	finish
	
	! Load CMATRIX results into a customized matrix
	*do,ii,1,ELTR_NUM-1 
		*do,jj,ii+1,ELTR_NUM
			/post1
			DATA_RES(ii,jj)=CMATRIX(ii,jj,2)
			DATA_RES_NORMAL(ii,jj)=(DATA_RES(ii,jj)-DATA_EMPTY(ii,jj))/(DATA_FULL(ii,jj)-DATA_EMPTY(ii,jj))*(epsr_plc-epsr_air)
			finish
		*enddo
	*enddo
	
	! Save ANSYS results into a file
	ELEM_DIV=ee ! element that been changed
	/com
	/OUTPUT,'ECT_RES','csv',,APPEND
	*VWRITE,ELEM_DIV     !ELEM_DIV is an integer, number of electrodes in the model
	%I
	! integer
	/OUTPUT,TERM ! stop writing
	
	/com
	/OUTPUT,'ECT_RES','csv',,APPEND
	*VWRITE,DATA_RES_NORMAL(1,2),DATA_RES_NORMAL(1,3),DATA_RES_NORMAL(1,4),DATA_RES_NORMAL(2,3),DATA_RES_NORMAL(2,4),DATA_RES_NORMAL(3,4)
	%G, %G, %G, %G, %G, %G
	! Double-precision floating
	/OUTPUT,TERM
	finish
	
	! Change it back
	/PREP7
	ESEL,s,,,ee
	EMODIF,all,MAT,1,
	ALLSEL,all
	finish
	
*enddo
